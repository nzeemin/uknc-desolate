
; Game main loop
;
L9DDD:
	MOVB	@#LDB7A, R0	; Get Health
	BNE	L9DDD1
	JMP	LB9A2		; zero => Player is dead
L9DDD1:
	CALL	LADE5		; Decode current room to LDBF5; HL = LDBF5
	CALL	LA88F		; Display 96 tiles on the screen
	CALL	LB96B		; Display Health
	CALL	LB8EA		; Show look/shoot selection indicator
	CALL	LB76B		; Process shoot
	CALL	LB551		; Process Alien in the room
; Process keys
	CLR	@#<L9E191+2>	; Clear key pressed stored for the game loop end
	CALL	LA0F1		; Scan keyboard
	BEQ	L9DDDE		; no key => skip the analysis
	CMP	R0, #004	; Up key?
	BEQ	L9DMUP
	CMP	R0, #001	; Down key?
	BEQ	L9DMDN
	CMP	R0, #002	; Left key?
	BEQ	L9DMLT
	CMP	R0, #003	; Right key?
	BEQ	L9DMRT
	CMP	R0, #15.	; Menu key?
	BEQ	L9DMNU
	MOV	R0, @#<L9E191+2>	; store key pressed for the game loop end
L9DDDE:	JMP	LA8C6		; Draw the Player, then go to the Ending of main game loop
;
L9DMDN: JMP	LA966		; 001 Move down
L9DMLT:	JMP	LA9EB		; 002 Move left
L9DMRT:	JMP	LAA1A		; 003 Move right
L9DMUP:	JMP	LA99B		; 004 Move up
L9DLST:	JMP	LAAAF		; 005 Look / Shoot
L9DINV:	JMP	LB0A2		; 006 Open the Inventory
L9DSWI:	JMP	LB930		; 010 Look / Shoot Mode switch
L9DMNU:	JMP	MENUFG		; Return to main Menu
;
; Ending of main game loop
L9E19:
	CALL	LB653		; Draw Alien
L9E191:	MOV	#000, R0	; key not processed yet
	BNE	L9E192
	CALL	LA0F1		; Scan keyboard
L9E192:	CMP	R0, #005	; Look/shoot key?
	BEQ	L9DLST
	CMP	R0, #010	; Switch key?
	BEQ	L9DSWI
	CMP	R0, #006	; Inventory key?
	BEQ	L9DINV
; Show the screen, continue the game main loop
L9E2E:
	CALL	SHOWSC		; Copy shadow screen to UKNC screen
	BR	L9DDD		; continue main game loop
;
; Quit menu item selected
L9E51:
;TODO
	RETURN
;
; Put tile on the screen (NOT aligned to 8px column), 16x16 -> 16x16 on shadow screen
; Uses XOR operation so it is revertable.
;   R3 = row; R2 = X coord; R1 = height; R4 = tile address
L9E5F:
	ADD	R3, R3
	MOV	MUL24D(R3), R3	; R3 = row * 24
	MOV	R2, R0		; get X coord, pixels
	BIC	#177770, R0	; offset within 8px column, 0..7
	ASR	R2
	ASR	R2
	ASR	R2		; R2 = number of 8px column
	ADD	R2, R3		; now R3 = offset on the shadow screen
	ADD	#SHADOW, R3	; R3 = address in the shadow screen
L9E8D:				; loop by R1 - by rows
	PUSH	R1
	MOV	(R4)+, R0	; get tile bits
	CLR	R2
	MOV	R5, R1		; get shift
	BEQ	L9E9D		; shift 0 => skip all shift ops
L9E96:				; shift bits
	ASR	R0
	RORB	R2
	SOB	R1, L9E96
L9E9D:
;TODO: put with XOR on the screen
	POP	R1
	SOB	R1, L9E8D	; continue loop by rows
	RETURN
;
; Put background tile on the screen, 16x16 -> 16x16 on shadow screen, no mask
;   R3 = row; R2 = tile column 0..11; R4 = tile address
L9EAD:
	ADD	R3, R3
	MOV	MUL24D(R3), R3	; R3 = row * 24
	ADD	R2, R3
	ADD	R2, R3
	ADD	#SHADOW, R3	; now R3 = shadow screen address
	MOV	#4., R1		; 4 * 4 = 16. rows
	MOV	#24., R2	; line increment
L9EAD1:
	MOV	(R4)+, (R3)	; 1st line
	ADD	R2, R3		; next line
	MOV	(R4)+, (R3)	; 2nd line
	ADD	R2, R3		; next line
	MOV	(R4)+, (R3)	; 3rd line
	ADD	R2, R3		; next line
	MOV	(R4)+, (R3)	; 4th line
	ADD	R2, R3		; next line
; Continue the loop
	SOB	R1, L9EAD1
	RETURN
;
; Draw sprite
;   R4 = sprite address; R2 = column; R3 = row
;   R0 = (was: bits 0..5 - sub-sprite); bit7=1 - reflect horz (was: bit6=1 - reflect vert)
L9EDE:
; Copy the sprite to the buffer
	MOV	#L9FAF, R5
	MOV	#8., R1
L9EDE1:	MOV	(R4)+, (R5)+
	MOV	(R4)+, (R5)+
	MOV	(R4)+, (R5)+
	MOV	(R4)+, (R5)+
	SOB	R1, L9EDE1
; Reflect the bits if needed
	BIT	#200, R0
	BEQ	L9EDE2
	CALL	L9EDER		; Reflect sprite horizontally
L9EDE2:
	ADD	R3, R3
	MOV	MUL24D(R3), R3	; R3 = row * 24
	ADD	R2, R3
	ADD	#SHADOW, R3
	MOV	#L9FAF, R4
; Copying to the shadow screen
	MOV	#8., R1		; 8 row pairs
	MOV	#24., R2	; line increment
L9EDE3:
; 1st line
	MOV	(R3), R0	; get shadow screen bits
	BIC	(R4)+, R0	; apply mask
	BIS	(R4)+, R0	; use pixels
	MOV	R0, (R3)	; write the result
	ADD	R2, R3		; next line
; 2nd line
	MOV	(R3), R0	; get shadow screen bits
	BIC	(R4)+, R0	; apply mask
	BIS	(R4)+, R0	; use pixels
	MOV	R0, (R3)	; write the result
	ADD	R2, R3		; next line
; Contine the loop
	SOB	R1, L9EDE3
	RETURN
L9FAF:
	.BLKB	64.
;
; Horizontal reflection
L9EDER:
	MOV	#L9FAF, R5
	MOV	#32., R4
L9EDES:
; Обменять мл/ст байты слова (R5), отразив оба байта
	MOV	(R5), R0
	MOV	R0, R1
	BIC	#177400, R0		; оставляем мл байт
	MOVB	FLIPAR(R0), R0		; отражаем байт через таблицу
	BIC	#177400, R0		; убираем возможное расширение знака
	SWAB	R1
	BIC	#177400, R1		; оставляем мл байт
	MOVB	FLIPAR(R1), R1		; отражаем байт через таблицу
	SWAB	R0
	BISB	R1, R0
	MOV	R0, (R5)+		; записываем результат и к следующему слову
; Продолжение цикла
	SOB	R4, L9EDES
	RETURN
;
; Copy shadow screen to UKNC screen
;
L9FEA=SHOWSC
;
; Clear shadow screen
; 128+12 lines, 24 8px columns; 24 * 140 = 3360 bytes
CLSHAD:
L9FCF:	MOV	#140., R1		; line count
	MOV	#SHADOW, R3
L9FCF1:	CLR	(R3)+			; bytes 0-1
	CLR	(R3)+			; 2-3
	CLR	(R3)+			; 4-5
	CLR	(R3)+			; 6-7
	CLR	(R3)+			; 8-9
	CLR	(R3)+			; 10-11
	CLR	(R3)+			; 12-13
	CLR	(R3)+			; 14-15
	CLR	(R3)+			; 16-17
	CLR	(R3)+			; 18-19
	CLR	(R3)+			; 20-21
	CLR	(R3)+			; 22-23
	SOB	R1, L9FCF1
	RETURN

; Scan keyboard
; Returns key in A; Z=0 for key, Z=1 for no key
;
LA0F1:
	CALL	GETKEY
	BEQ	LA0F1R		; No key => RETURN
; Анализ нажатой клавиши
	CMP	#040, R0	; Клавиша пробел?
	BNE	LA0F11
	MOV	#005, R0
LA0F11:	CMP	#15502, R0	; Клавиша вниз?
	BNE	LA0F12
	MOV	#001, R0
	RETURN
LA0F12:	CMP	#15504, R0	; Клавиша влево?
	BNE	LA0F13
	MOV	#002, R0
	RETURN
LA0F13:	CMP	#15503, R0	; Клавиша вправо?
	BNE	LA0F14
	MOV	#003, R0
	RETURN
LA0F14:	CMP	#15501, R0	; Клавиша вверх?
	BNE	LA0F15
	MOV	#004, R0
	RETURN
LA0F15:
;TODO: Escape AR2
LA0F16:	CMP	#111, R0	; Inventory key? 'I'
	BNE	LA0F17
	MOV	#006, R0
	RETURN
LA0F17:	CMP	#120, R0	; Menu key? 'P'
	BNE	LA0F18
	MOV	#15., R0
	RETURN
LA0F18:
	CLR	R0
LA0F1R:	RETURN
;
; Display 96 tiles on the screen with background tiles (Tileset1)
;   R3 = Address where the 96 tiles are placed
LA88F:
	CLR	R1		; col
	CLR	R2		; row
LA892:	PUSH	R1
	PUSH	R3
	PUSH	R2
	MOVB	(R3), R4
	BEQ	LA8B0		; empty tile? => skip it
	CMP	R4, #107	; menu background tile?
	BNE	LA8921		; no => skip
LA88F2:	ADD	#000, R4	; add phase 000..007
LA8921:	ADD	R4, R4
	ADD	R4, R4
	ADD	R4, R4
	ADD	R4, R4
	ADD	R4, R4		; now R4 = tile * 32
	ADD	#TILES1, R4
	MOV	R2, R3		; row
	MOV	R1, R2		; col
	CALL	L9EAD		; Put tile; R3 = row; R2 = col; R4 = tile address
LA8B0:	POP	R2
	POP	R3
	POP	R1
	INC	R3		; next tile
	INC	R1		; next col
	CMP	R1, #12.	; was last column?
	BNE	LA892		; no => continue the loop
	CLR	R1
	ADD	#16., R2	; next row
	CMPB	#128., R2	; was last tile row?
	BNE	LA892		; no => continue the loop
	RETURN
LDC55=LA88F2+2		; Menu background phase: 000..007
;
LA8C6:
	CLRB	@#LDD54		; clear animation phase
;
; Draw Player tiles
LA8CD:
	MOVB	@#LDD55, R0	; get shooting flag
	BEQ	LA8DF		; shooting animation? no => jump
	MOV	#LDE87, R3	; Table with Player's tile numbers
	MOV	@#LDB75, R0	; Direction/orientation
	ADD	R0, R0
	ADD	R0, R0		; now R0 = Direction * 4
	BR	LA8E9
LA8DF:
	MOV	#LDE47, R3	; Table with Player's tile numbers
	MOVB	@#LDB75, R0	; Direction/orientation
	ADD	R0, R0
	ADD	R0, R0
	ADD	R0, R0
	ADD	R0, R0		; now R0 = Direction * 16
LA8E9:
	ADD	R0, R3
	MOVB	@#LDD54, R0	; get animation phase
	ADD	R0, R0
	ADD	R0, R0		; now R0 = phase * 4
	ADD	R0, R3
	MOV	#4, R1		; 4 sprites
LA8F8:				; loop by R1
	MOVB	(R3)+, R4	; get sprite number
	PUSH	R3
	ADD	R4, R4
	ADD	R4, R4
	ADD	R4, R4
	ADD	R4, R4
	ADD	R4, R4
	ADD	R4, R4		; R4 = sprite * 64
	ADD	#SPRITE, R4	; now R4 = sprite address
	CALL	LA92E
	PUSH	R1
	CALL	LA956		; if looking left - set C bit7=1 to reflect tile horizontal
	MOV	R1, R0
	CALL	L9EDE		; Draw sprite R4; R2 = column; R3 = row
	POP	R1
	POP	R3
	SOB	R1, LA8F8	; continue loop by tiles
	MOVB	@#LDD54, R0	; get animation phase 0..3
	CMP	R0, #003	; was last phase?
	BEQ	LA927		; yes => jump
	INC	R0		; next phase
	MOVB	R0, @#LDD54	; set animation phase
	CLRB	@#LDD55		; clear shooting flag for player's animation
	JMP	L9E19		; Go to ending of main game loop
LA927:
	CLRB	@#LDD54		; clear animation phase
	JMP	L9E19		; Go to ending of main game loop
;
LA92E:
	MOVB	@#LDB76, R2	; get X coord in tiles 0..11
	ADD	R2, R2		; now coord in 8px columns
	MOVB	@#LDB77, R3	; get Y coord in lines
	SUB	#16., R3
	CMP	R1, #4		; sprite #1?
	BEQ	LA949		; yes => RETURN
	CMP	R1, #3		; sprite #2?
	BNE	LA94C		;
LA941:
	MOVB	@#LDB75, R0	; Direction/orientation
	CMPB	R0, #002	; left?
	BEQ	LA94A
	ADD	#2, R2		; right
LA949:	RETURN
LA94A:
	SUB	#2, R2		; left
	RETURN
LA94C:
	ADD	#16., R3
	CMP	R1, #1		; sprite #4?
	BEQ	LA941
	RETURN
;
LA956:
	CLR	R1
	MOVB	@#LDB75, R0	; Direction/orientation
	BEQ	LA956R
	CMP	R0, #001
	BEQ	LA956R
	CMP	R0, #003
	BEQ	LA956R
	MOV	#200, R1
LA956R:	RETURN

;
; Move Down
LA966:
	TSTB	@#LDB75		; Direction/orientation
	BEQ	LA97C		; down? => jump
	TSTB	@#LDB7D		; look/shoot switch
	BEQ	LA97C		; look mode => jump
	CLRB	@#LDB75		; set Direction/orientation = down
	JMP	LA8C6		; Proceed to Draw the Player
LA97C:
	CLRB	@#LDB75		; set Direction/orientation = down
	CALL	LAA60		; get byte from the room after movement
	CMP	R0, #001	; free block?
	BEQ	LA97C1		; yes => move
	JMP	LA8CD
LA97C1:	MOVB	@#LDB77, R0	; get Y pixel coord
	PUSH	R0
	ADD	#16., R0	; Down one tile
	MOVB	R0, @#LDB77	; set Y pixel coord
	MOVB	@#LDB78, R0	; get Y tile coord
	PUSH	R0
	INC	R0		; down one tile
	MOVB	R0, @#LDB78	; set Y tile coord
	BR 	LA9D1
;
; Move Up
LA99B:
	MOVB	@#LDB75, R0	; Direction/orientation
	CMP	R0, #001	; up?
	BEQ	LA9B3
	TSTB	@#LDB7D		; look/shoot switch
	BEQ	LA9B3		; look mode => jump
	MOVB	#001, @#LDB75	; set Direction/orientation = up
	JMP	LA8C6		; Proceed to Draw the Player
LA9B3:
	MOVB	#001, @#LDB75	; set Direction/orientation = up
	CALL	LAA60		; get byte from the room after movement
	CMP	R0, #001
	BEQ	LA9B31
	JMP	LA8CD
LA9B31:	MOVB	@#LDB77, R0	; get Y pixel coord
	PUSH	R0
	SUB	#16., R0	; Up one tile
	MOVB	R0, @#LDB77	; set Y pixel coord
	MOVB	@#LDB78, R0	; get Y tile coord
	PUSH	R0
	DEC	R0		; up one tile
	MOVB	R0, @#LDB78	; set Y tile coord
; Moved down or up, check for Alien
LA9D1:
	TSTB	@#LDB84		; Alien still alive?
	BEQ	LA9E6		; dead => jump
	CALL	LB72E		; Get value at offset $2F in the room description
	BEQ	LA9E6		; no alien => jump
; We have an alien in the room
	CALL	LB74C
	BNE	LA9E6
	JMP	LB07B		; Alien in the same cell as Player? => Decrease Health by 4, restore X coord
LA9E6:
	POP	R0
	POP	R0
	JMP	LA8CD
;
; Move Left
LA9EB:
	MOVB	@#LDB75, R0	; Direction/orientation
	CMP	R0, #002	; up?
	BEQ	LAA03
	TSTB	@#LDB7D		; look/shoot switch
	BEQ	LAA03		; look mode => jump
	MOVB	#002, @#LDB75	; set Direction/orientation = left
	JMP	LA8C6		; Proceed to Draw the Player
LAA03:
	MOVB	#002, @#LDB75	; set Direction/orientation = left
	CALL	LAA60		; get byte from the room after movement
	CMP	R0, #001
	BEQ	LAA031
	JMP	LA8CD
LAA031:	MOVB	@#LDB76, R0	; get X coord in tiles
	PUSH	R0
	DEC	R0		; one tile left
	MOVB	R0, @#LDB76	; set X coord in tiles
	BR	LAA47		; go to Alien check
;
; Move Right
LAA1A:
	MOVB	@#LDB75, R0	; Direction/orientation
	CMP	R0, #003	; up?
	BEQ	LAA32
	TSTB	@#LDB7D		; look/shoot switch
	BEQ	LAA32		; look mode => jump
	MOVB	#003, @#LDB75	; set Direction/orientation = right
	JMP	LA8C6		; Proceed to Draw the Player
LAA32:
	MOVB	#003, @#LDB75	; set Direction/orientation = right
	CALL	LAA60		; get byte from the room after movement
	CMP	R0, #001
	BEQ	LAA321
	JMP	LA8CD
LAA321:	MOVB	@#LDB76, R0	; get X coord in tiles
	PUSH	R0
	INC	R0		; one tile right
	MOVB	R0, @#LDB76	; set X coord in tiles
; Moved left or right, check for Alien
LAA47:
	TSTB	@#LDB84		; Alien still alive?
	BEQ	LAA5C		; dead => skip
	CALL	LB72E		; Get value at offset $2F in the room description
	BEQ	LAA5C		; no Alien => jump
; We have an alien in the room
	CALL	LB74C
	BNE	LAA5C
	JMP	LB08D		; Alien in the same cell as Player? => Decrease Health by 4, restore X coord
LAA5C:
	POP	R0
	JMP	LA8CD
;
; Get byte from the room at position after the movement
;   R3 = byte from the room
LAA60:
	CALL	LADE5		; Decode current room to LDBF5; R3 = LDBF5
	MOVB	@#LDB76, R2	; get X coord in tiles
	CALL	LAA7D		; For direction left - dec R2, right - inc R2
	ADD	R2, R3
	MOVB	@#LDB78, R1	; Get Y tile coord
	CALL	LAA8D		; For direction up - dec R1, down - inc R1
	MOVB	MUL12D(R1), R1	; multiply by 12
	ADD	R1, R3		; now R3 = address in the room
	MOVB	(R3), R0	; get byte from the room
	RETURN
;
; For direction left - dec R2, right - inc R2
LAA7D:
	MOVB	@#LDB75, R0	; Direction/orientation
	BEQ	LAA7DR		; down? => RETURN
	CMP	R0, #001	; up?
	BEQ	LAA7DR		; => RETURN
	CMP	R0, #002	; left?
	BNE	LAA8B
	DEC	R2		; going left 1 tile
	RETURN
LAA8B:	INC	R2		; going right 1 tile
LAA7DR:	RETURN
;
; For direction up - dec R1, down - inc R1
LAA8D:
	MOVB	@#LDB75, R0	; Direction/orientation
	BNE	LAA8D1		; down?
	INC	R1		; going down 1 tile
	RETURN
LAA8D1:	CMP	R0, #001	; up?
	BNE	LAA8DR
	DEC	R1		; going up 1 tile
LAA8DR:	RETURN
;
; Get room offset in tiles for X = LDB76, Y = LDB78
;   Returns the room offset in R0 and LDC56
LAA9D:
	MOVB	@#LDB78, R0	; get Y tile coord
	MOVB	MUL12D(R0), R0	; multiply by 12
	MOVB	@#LDB76, R2	; get X tile coord 0..11
	ADD	R2, R0
	MOVB	R0, @#LDC56	; (LDC56) = Y * 12 + X
	RETURN
;
; Look / Shoot key pressed
LAAAF:
	TSTB	@#LDB7D		; look/shoot switch value
	BEQ	LAAAF1		; look => jump
	JMP	LB758		; shoot
; Look action
LAAAF1:
	CLRB	@#LDC88		; clear current offset
	CALL	LAA9D		; Get room offset in tiles for X = LDB76, Y = LDB78
	CALL	LAE09		; Decode current room description to LDBF5
LAAC1:
	MOVB	(R3), R1
	MOVB	@#LDC56, R0
	SUB	R1, R0
	BEQ	LAADD		; found the action point for the current position
	MOVB	@#LDC88, R0	; get current offset
	CMP	R0, #061
	BEQ	LAADA		; => Show the screen, continue the game main loop
	INC	R0
	MOVB	R0, @#LDC88	; set current offset
	INC	R3
	BR	LAAC1
; Show the screen, continue the game main loop
LAADA:
	JMP	L9E2E		; Show the screen, continue the game main loop
; Found the action point for the current position in the room description
LAADD:
	MOVB	@#LDC88, R0	; get current offset in the room description
	BEQ	LAB3F
	CMP	R0, #001
	BEQ	LAB3F
	CMP	R0, #003
	BEQ	LABA4
	CMP	R0, #004
	BEQ	LABA4
	CMP	R0, #031
	BEQ	LABBE
	CMP	R0, #032
	BEQ	LABBE
	CMP	R0, #041
	BEQ	LAADD4
	CMP	R0, #042
	BEQ	LAADD4
	CMP	R0, #006
	BEQ	LAADD5
	CMP	R0, #007
	BEQ	LAADD5
	CMP	R0, #013
	BEQ	LAADD6
	CMP	R0, #014
	BEQ	LAADD6
	CMP	R0, #017
	BEQ	LAADD7
	CMP	R0, #020
	BEQ	LAADD7
	JMP	L9E2E		; Show the screen, continue the game main loop
;
LAADD4:	JMP	LAC05
LAADD5:	JMP	LAC54
LAADD6:	JMP	LACE3
LAADD7:	JMP	LBC8B
;
; Show small message popup
LAB28:
	PUSH	R1
	PUSh	R2
	MOV	#LEB27, R3	; Decode from: Small message popup
	CALL	LADEE		; Decode 96 bytes of the screen to LDBF5
	CALL	LB177		; Display screen HL from tiles with Tileset2
	POP	R2
	POP	R1
	RETURN
;
; Found action point at room description offset $00..$01
LAB3F:
	CALL	LAE09		; Decode current room description to LDBF5
	MOV	#002, R2	; offset in the room description
	CALL	LAC4C		; Compare byte at (R3+R2) with Direction/orientation LDB75
	BEQ	LAB3F1
	JMP	LAADA		; => Show the screen, continue the game main loop
LAB3F1:	CMPB	@#LDB79, #27.	; Room #27 ?
	BNE	LAB7A		; no => jump
; Room #27
	TSTB	@#LDCF7		; Weapon - do we have it?
	BNE	LAB7A		; have weapon => jump
	MOVB	#22., @#LDCF3	; Left margin size for text
	MOVB	#14., @#LDCF4	; Line interval for text
	CALL	LAB28		; Show small message popup
	CALL	LAB73		; Set penRow/penCol for small message popup
	MOV	#SE0D5, R3	; "It is not wise to proceed without a weapon."
	CALL	LBEDE		;  Show message char-by-char
	JMP	LAD8C		; Show screen and wait for Escape key
;
; Set penRow/penCol for small message popup
LAB73:
	MOV	#54020, @#L86D7	; Set penRow/penCol
	RETURN
;
LAB7A:
	MOVB	#001, @#LDC8A	; Direction to other room = down
	CALL	LAE09		; Decode current room description to LDBF5
	MOV	#034, R2	; offset in the room description - access level
LAB85:
	ADD	R2, R3
	MOVB	(R3), @#LDC8C	; Set Access code level
	ADD	#007, R3	; R3 = $1C+7=$23 - offset for room number
	MOVB	(R3), @#LDC86	; store new room number
	ADD	#004, R3	; R3 = $1C+7+4=$27 - offset for Access code slot
	MOVB	(R3), @#LDC8B	; set Access code slot number
	TSTB	@#LDC8C		; Level 0?
	BNE	LAB851
	JMP	LB00E		; yes => Going to the next room
LAB851:	JMP	LAE23		; Check access and show Door Lock
;
; Found action point at room description offset $03..$04
LABA4:
	CALL	LAE09		; Decode current room description to LDBF5
	MOV	#005, R2	; offset in the room description - direction
	CALL	LAC4C		; Compare byte at (HL+DE) with Direction/orientation LDB75
	BEQ	LABA41
	JMP	LAADA		; => Show the screen, continue the game main loop
LABA41:	MOVB	#002, @#LDC8A	; Direction to other room = up
	CALL	LAE09		; Decode current room description to LDBF5
	MOV	#29., R2	; offset in the room description
	BR	LAB85
;
; Found action point at room description offset $19..$1A
LABBE:
	CALL	LAE09		; Decode current room description to LDBF5
	MOV	#27., R2	; offset in the room description - direction byte
	CALL	LAC4C		; Compare byte at (R3+R2) with Direction/orientation LDB75
	BEQ	LABBE1
	JMP	LAADA		; => Show the screen, continue the game main loop
LABBE1:	CMPB	@#LDB79, #33.	; Room #33?
	BNE	LABF7
; Room #33
	MOV	#004, R2
	CALL	LB531		; Get value (LDB90+R2)
	BNE	LABF7
	MOVB	#16., @#LDCF3	; Left margin size for text
	MOVB	#14., @#LDCF4	; Line interval for text
	CALL	LAB28		; Show small message popup
	MOV	#54014, @#L86D7	; Set penRow/penCol
	MOV	#SE0D7, R3	; "You cant enter that sector Life-Support is offline."
	CALL	LBEDE		; Show message char-by-char
	JMP	LAD8C		; Show screen and wait for Escape key
LABF7:
	MOVB	#003, @#LDC8A	; Direction to other room = left
	CALL	LAE09		; Decode current room description to LDBF5
	JMP	LAB85
;
; Found action point at room description offset $21..$22 (possibly an error, should be $20-$21)
LAC05:
	CALL	LAE09		; Decode current room description to LDBF5
	MOV	#042, R2	; offset in the room description
	CALL	LAC4C		; Compare byte at (HL+DE) with Direction/orientation LDB75
	BEQ	LAC051
	JMP	LAADA		; => Show the screen, continue the game main loop
LAC051:	CMPB	@#LDB79, #69.	; Room #69?
	BNE	LAC3E		; no => jump
; Room #69
	MOV	#005, R2
	CALL	LB531		; Get value (LDB90+R2)
	BNE	LAC3E
	MOVB	#12., @#LDCF3	; Left margin size for text
	MOVB	#14., @#LDCF4	; Line interval for text
	CALL	LAB28		; Show small message popup
	MOV	#54024, @#L86D7	; Set penRow/penCol
	MOV	#SE0D9, R3	; "You cant enter until the AirLock is re-pressurised"
	CALL	LBEDE		; Show message char-by-char
	JMP	LAD8C		; Show screen and wait for Escape key
LAC3E:
	MOVB	#004, @#LDC8A	; Direction to other room = right
	CALL	LAE09		; Decode current room description to LDBF5
	MOV	#037, R2	; offset in the room description
	JMP	LAB85
;
; Compare byte at (HL+DE) with Direction/orientation LDB75
LAC4C:
	ADD	R2, R3
	MOVB	(R3), R1
	MOVB	@#LDB75, R0
	SUB	R1, R0
	RETURN
;
; Found action point at room description offset $06..$07
LAC54:
;TODO
; Found dead body with some item on it
LAC97:
;TODO
	JMP	LAD00
;
; Show arrow sign in bottom-right corner, as a prompt to continue
LACB8:
	MOV	#063260, @#L86D7	; Set penRow/penCol
	MOV	#SE0B9, R3	; String with arrow down sign
	JMP	LBEDE		; Show message char-by-char
;
; Small message popup "OMG! This Person Is DEAD! What Happened Here!?!"
LACC5:
	CALL	LAB28		; Show small message popup
	CALL	LAB73		; Set penRow/penCol for small message popup
	MOV	#SE0BF, R3
	CALL	LBEDE		; Show message char-by-char
	MOV	#63022, @#L86D7	; Set penRow/penCol
	MOV	#SE0C1, R3	; "What Happened Here!?!"
	CALL	LBEDE		; Show message char-by-char
	CALL	LACB8		; Show arrow sign as prompt to continue
	JMP	LAD00
;
; Found action point at room description offset $0B..$0C
LACE3:
;TODO
;
; Show screen, wait for any key, show small message popup
LACF6:
	CALL	SHOWSC		; Copy shadow screen to UKNC screen
	CALL	WAITAN		; Wait any key
	JMP	LAB28		; Show small message popup
;
; Get item found on the dead body
LAD00:
	CALL	LACF6		; Show screen, wait for any key, show small message popup
	MOV	#54026, @#L86D7	; Set penRow/penCol
	MOV	#SE0C9, R3	; "They Seem To Be Holding"
	CALL	LBEDE		; Show message char-by-char
	MOV	#63076, @#L86D7	; Set penRow/penCol
	MOV	#SE0CB, R3	; "Something"
	CALL	LBEDE		; Show message char-by-char
	INCB	@#LDBC7		; increase Items Found count
LAD22:
	CALL	LACB8		; Show arrow sign in bottom-right corner
	CALL	LACF6		; Show screen, wait for any key, show small message popup
	MOV	#54060, @#L86D7	; Set penRow/penCol
	MOV	#SE0CF, R3	; "You Picked Up A"
	CALL	LBEDE		; Show message char-by-char
	MOV	#63022, @#L86D7	; Set penRow/penCol
	CALL	LAE19		; Get inventory item description string
	CALL	LBEDE		; Show message char-by-char
	MOVB	@#LDC89, R3	; get the current item number
	ADD	#LDB9C, R3	; R3 = item address in my Inventory
	MOVB	#001, (R3)	; Mark that we have the item now
	JMP	LAD8C		; Show screen and wait for Escape key
;
; Get inventory item flag for item number in LDC89
LAD4F:
	MOVB	@#LDC89, R3	; get current item number
	ADD	#LDB9C, R3	; R3 = item address in my Inventory
	MOVB	(R3), R0	; R0 = item flag: $00 = not having, $01 = have it
	RETURN
;
LAD5B:
	CALL	LAE09		; Decode current room description to LDBF5
	ADD	#14., R3	; + offset in the room description
	MOVB	(R3), R0
	MOVB	R0, @#LDC89	; set as current item
	CMP	R0, #43		; weapon?
	BEQ	LADA9		; yes => pick it up
	CALL	LAD4F		; Get inventory item flag for item number in LDC89
	CMP	R0, #001	; do we have it?
	BNE	LAD5B1
	JMP	LAADA		; yes => Show the screen, continue the game main loop
LAD5B1:	CALL	LAB28		; Show small message popup
	MOV	#54026, @#L86D7	; Set penRow/penCol
	MOV	#SE0CD, R3	; " Hey Whats This . . . ?"
	CALL	LBEDE		; Show message char-by-char
	INCB	@#LDBC7		; increase Items Found count
	JMP	LAD22
;
; Show screen and wait for Escape key
LAD8C:
	CALL	SHOWSC		; Copy shadow screen to UKNC screen
LAD8F:
	CALL	WTKEY	;STUB
	JMP	L9E2E		; Show the screen, continue the game main loop
;
; Wait for Escape key
LADA1:
	CALL	LA0F1		; Scan keyboard
	CALL	WTKEY	;STUB
	RETURN
;
; We found the weapon
LADA9:
;TODO
	RETURN
;
; Decode current room to LDBF5
;   Returns: R3 = LDBF5
LADE5:
	MOVB	@#LDB79, R0	; Get the room number
	MOV	#LDE97, R3	; List of encoded room addresses
	CALL	LADFF		; now R3 = encoded room address
; Entry point: Decode 96 bytes to LDBF5
LADEE:
	MOV	#96., R1	; decode 96 bytes
; Decode the room/screen to LDBF5
;   R3 = decode from; R1 = tile count to decode
;   Returns: R3 = LDBF5
LADF5:
	MOV	#LDBF5, R2
	CALL	LB9F1		; Decode the room/screen
	MOV	#LDBF5, R3
	RETURN
;
; Get address from table
;   R0 = Element number
;   R3 = Table address
;TODO: Replace CALL LADFF to intermediate commands
LADFF:
	ADD	R0, R0
	ADD	R0, R3
	MOV	(R3), R3
	RETURN
;
; Decode current room description to LDBF5
;   Returns: R3 = LDBF5
LAE09:
	MOVB	@#LDB79, R0	; Get room number
	MOV	#LDF27, R3	; Table of adresses for room descriptions
	CALL	LADFF		; Get address from table by index A
	MOV	#49., R1	; decode 49 bytes
	BR	LADF5		; Decode the room/screen to LDBF5
;
; Inventory item to item description string
LAE19:
	MOVB	@#LDC89, R3	; get current item number
	ADD	R3, R3
	MOV	LDFB7(R3), R3	; Get address from table LDFB7 by index
	RETURN
;
; Check access and show Door Lock
;   LDC8B - Access code slot number
LAE23:
	MOVB	#50., @#LDC59	; set delay factor
	MOVB	@#LDC8B, R3	; get Access code slot number
	ADD	#LDCA2, R3	; Table with Access code slots
	MOVB	(R3), R0	; get value from the slot
	BEQ	LAE231
	JMP	LB00E		; code was entered already? => Going to the next room
; Need to enter the code for the slot
LAE231:
	MOV	#004, R1	; 4 symbols in the code
	MOV	#LDC8D, R3	; Buffer for entering access code
LAE3D:
	CLRB	(R3)+
	SOB	R1, LAE3D
	MOV	#LF468, R3	; Encoded screen: Door Lock panel popup
	CALL	LADEE		; Decode 96 bytes of the screen to DBF5
	CALL	LB177		; Display screen R3 from tiles with Tileset2
	MOVB	#10., @#LDCF3	; Left margin size for text
	MOVB	#12., @#LDCF4	; Line interval for text
	CALL	LB09B		; Preparing to draw string with the prompt
	MOV	#SE0DD, R3	; "Door Locked"
	CALL	LBEDE		; Show message char-by-char
	MOV	#42012, @#L86D7	; Set penRow/penCol
	CALL	LAFFE		; Get "Access code level N required" string by access level in DC8C
	CALL	LBEDE		; Show message HL char-by-char

;TODO
; Door Lock loop starts here
LAE80:
;TODO
; Delay and wait for key in Door Lock
LAE99:
;TODO
; Door Lock Select key pressed
LAEBA:
;TODO
	RETURN
;
; Access code entered, need to check
LAF14:
;TODO
; Invalid Code
LAF1D:
;TODO
; Validate the code entered
LAF2C:
;TODO
	RETURN
;
; Draw selection box by XOR
LAFD2:
;TODO
	RETURN
;
; LDC8C access code level -> address from LE015 table
LAFEC:
;TODO
	RETURN
;
; LDC8C access code level -> message address from LE01F table
LAFFE:
;TODO
	RETURN
;
; LDC8C access code level -> "access code was entered for this level before" flag address
; Returns: HL = address
; DoorLockGetEnteredFlagAddr:
DRGEFA:
;TODO
	RETURN
;
; Going to the next room
LB00E:
;TODO
	RETURN
;
; Decrease Health by 4, restore Y coord
LB07B:
	MOV	#2, R1
LB07D:
	CALL	LB994		; Decrease Health
	SOB	R1, LB07D
	POP	R0
	MOVB	R0, @#LDB78	; set Y tile coord
	POP	R0
	MOVB	R0, @#LDB77	; set Y pixel coord
	JMP	LA8CD
;
; Decrease Health by 4, restore X coord
LB08D:
	MOV	#2, R1
LB08F:
	CALL	LB994		; Decrease Health
	SOB	R1, LB08F
	POP	R0
	MOVB	R0, @#LDB76
	JMP	LA8CD
;
; Door Lock: Preparing to draw string with prompt/result
LB09B:
	MOV	#32001, R3	; line 52 col 1
	MOV	#6014, R2	; 12 rows, 12 cols
	CALL	CLBLK		; ClearScreenBlock
	MOV	#32014, @#L86D7	; set penRow/penCol
	RETURN
;
; Open the Inventory pop-up
;
LB0A2:
	MOV	#LF329, R3	; Encoded screen for Inventory/Info popup
	CALL	LADEE		; Decode 96 bytes of the screen to LDBF5
	CALL	LB177		; Display screen HL from tiles with Tileset 2
	MOVB	#22., @#LDCF3	; Left margin size for text
	MOVB	#12., @#LDCF4	; Line interval for text
	CLRB	@#LDCF5		; Data cartridge reader slot??
	CLRB	@#LDC59		; set delay factor
	CLRB	@#LDC5A		; clear Inventory items count
	CLRB	@#LDCF8
	MOVB	#16., @#LDC83	; set X pos
	MOVB	#24., @#LDC84	; set Y pos
	MOV	#13060, @#L86D7	; Set penRow/penCol
	MOV	#SE0BB, R3	; "- INVENTORY -"
	CALL	DRSTR
	MOV	#LDB9C, R3
	MOV	#29., R1
LB0E0:				; loop by B
	PUSH	R3
	MOVB	(R3), R0	; get item
	BEQ	LB0E01		; no item => skip
	CALL	LB12A		; we have the item => put in the list and draw
LB0E01:	POP	R3
	INC	R3
	SOB	R1, LB0E0
	MOV	#30., R1	; 30 placeholders
	MOVB	@#LDC5A, R0	; get Inventory items count
	SUB	R0, R1		; R1 = count of empty slots
LB0F3:
	PUSH	R1
	MOV	#<TILES3+448.>, R4	; Tile gray dot in the center - placeholder
	CALL	LB15D		; Draw tile by XOR then go to next position
	POP	R1
	BEQ	LB119		; last item => exit the loop
	DEC	R1
	MOV	#LDC5B, R3	; Inventory list
	MOVB	@#LDC5A, R2	; get Inventory items count
	ADD	R2, R3		; R3 = addr of the empty slot
	MOVB	#143, (R3)	; empty slot marker
	INCB	@#LDC5A		; increase Inventory items count
	BR	LB0F3
LB119:
	JMP	LB1AA		; go to Inventory screen loop
;
LB11C:
;TODO
	RETURN
;
LB12A:
;TODO
	RETURN

;
; Draw tile by XOR using X = (LDC83), Y = (LDC84), then go to next position
LB15D:
	MOVB	@#LDC84, R3	; get Y pos for Inventory
	MOVB	@#LDC83, R2	; get X pos for Inventory
	MOV	#16., R1
	CALL	L9E5F		; Draw tile by XOR operation
	MOVB	@#LDC83, R0	; get X pos
	ADD	#16., R0	; increase X
	MOVB	R0, @#LDC83	; set X pos
	CMP	R0, #176	;
	BNE	LB15DR
	CALL	LB11C
LB15DR:	RETURN
;
; Display screen from tiles with Tileset2
;   R3 = Screen in tiles, usually LDBF5
LB177:
	CLR	R1		; col
	CLR	R2		; row
LB1770:
	PUSH	R1
	PUSH	R2
	MOVB	(R3)+, R4
	PUSH	R3		; store tile address
	CMPB	#001, R4
	BEQ	LB1771
	ADD	R4, R4
	ADD	R4, R4
	ADD	R4, R4
	ADD	R4, R4
	ADD	R4, R4
	ADD	R4, R4		; now R4 = tile * 64
	ADD	#TILES2, R4	; R4 = tile address
	MOV	R2, R3		; row
	MOV	R1, R2		; col
	CALL	DRTIL2		; Draw tile from Tileset2; R3 = row, R2 = col, R4 = tile addr
LB1771:	POP	R3		; restore tile address
	POP	R2		; restore row
	POP	R1		; restore col
	ADD	#16., R1	; next col
	CMP	R1, #<16.*12.>	; end of line?
	BNE	LB1770		; no => continue the loop
	CLR	R1
	ADD	#16., R2	; next row
	CMPB	#128., R2	; last row?
	BNE	LB1770		; no => continue the loop
	RETURN
;
; Clear the bottom area in the Inventory popup
ClearInventoryMesage:
;TODO
	RETURN
;
; Inventory
LB1AA:
;TODO
LB1BB:				; Inventory loop starts here
;TODO
; Inventory item selection loop
LB1C1:
;TODO
LB1DB:
;TODO
	RETURN
LB1E2:
;TODO
	RETURN
LB1F0:
;TODO
	RETURN


; Delay by LDC59
LB2D0:
	MOVB	@#LDC59, R2
LB2D00:

;TODO
	RETURN
;
; Get value (LDB90+R2)
LB531:
	MOVB	LDB90(R2), R0
	RETURN
;
; Get value at $13 offset in the room description
LB538:
	CALL	LAE09		; Decode current room description to LDBF5
	ADD	#023, R3	; add offset in the room description
	MOVB	(R3), R0
	RETURN
;
; Get value at $0F offset in the room description
;   Returns: R1 = value from the offset; R0 = LDC56 = offset in the room
LB541:
	CALL	LAA9D		; Get room offset in tiles for X = LDB76, Y = LDB78
	CALL	LAE09		; Decode current room description to LDBF5
	ADD	#15., R3	; add offset in the room description
	MOVB	(R3), R1
	MOVB	@#LDC56, R0	; get offset in the room
	RETURN
;
; Process alien in the room
LB551:
;TODO
	RETURN

;
LB622:
;TODO
	RETURN

LB653:
;TODO
	RETURN

;
; Get A = Alien position within the room
LB6ED:
;TODO
	RETURN

;
; Get value at offset $2F in the room description
;   Returns: A = value
LB72E:
	CALL	LAE09		; Decode current room description to LDBF5
	ADD	#47., R3	; offset in the room description
	MOVB	(R3), R0
	RETURN
;
; Player injured by reflected bullet
LB737:
	CLRB	@#LDB8D		; clear shooting process mark
	CALL	LB994		; Decrease Health
	CMPB	@#LDB81, #002	; Alien type = big one?
	BNE	LB7371
	CALL	LB994		; Decrease Health
LB7371:	JMP	LB622
;
LB74C:
	CALL	LAA9D		; Get room offset in tiles for X = LDB76, Y = LDB78
	CALL	LB6ED		; Get A = Alien position within the room
	MOV	R0, R1
	MOVB	@#LDC56, R0	; get offset in the room
	SUB	R1, R0
	RETURN
;
; Fire key pressed in Shoot mode
LB758:
;TODO
LB768:
	JMP	L9E2E		; Show the screen, continue the game main loop
;
; Process shoot within the game main loop
;
LB76B:
;TODO
	RETURN

;
; Show look/shoot selection indicator
;
LB8EA:
;TODO
	RETURN

;
; Switch Look / Shoot mode
LB930:
;TODO
	JMP	L9E2E		; Show the screen, continue the game main loop

;
; Display Health
LB96B:
;TODO: ;DEBUG: Show room number at the bottom-left
	MOV	#454, @#L86D7	; Set penRow/penCol
	MOV	@#LDB7A, R3	; get Health value
	JMP	DRNUM3		; Show 3-digit decimal number R3
	RETURN
;
; Draw 5-digit number R3 at row/col R2, and show the screen
LB97D:
	MOV	R2, @#L86D7	; set penRow/penCol
	CALL	DRNUM5
	JMP	SHOWSC		; Copy shadow screen to ZX screen
;
; Decrease Health
LB994:
	MOVB	@#LDB7A, R0	; get Health
	SUB	#2, R0		; Health = Health minus 2
	BHIS	LB9941
	CLR	R0
LB9941:	MOVB	R0, @#LDB7A	; set Health
; Set border to red as an indication of the injury
;TODO: Indicate we had injures
	RETURN
;
; Player is dead, Health 0
;
LB9A2:
	CALL	CLSHAD		; Clear Shadow Screen
;TODO
	RETURN
;
; Clear player variables
LB9D6:
	CLRB	@#LDB79		; set the room number
	CLRB	@#LDB75		; Direction/orientation
	MOVB	#6, @#LDB76	; set X tile coord = 6
	MOVB	#48., @#LDB77	; set Y pixel coord = 48
	MOVB	#3, @#LDB78	; set Y tile coord = 3
	MOV	#100.,@#LDB7A	; set Health = 100
	RETURN
;
; Decode the block from RLE sequence
;   R3 = address decode from (usually encoded room/screen)
;   R2 = address decode to
;   R1 = number of bytes to decode
LB9F1:	MOVB	(R3)+, R0
	CMPB	R0, #377	; repeater?
	BEQ	LB9F13
	MOVB	R0, (R2)+
	SOB	R1, LB9F1
LB9F11:	RETURN
LB9F13:	MOVB	(R3)+, R0
	BIC	#177400, R0
	INC	R3
LB9F14:	DEC	R3
	MOVB	(R3)+, (R2)+
	DEC	R1
	BEQ	LB9F11
	SOB	R0, LB9F14
	BR	LB9F1
;
; Show titles and show Menu
LBA07:
	MOVB	#68., @#LDC59	; set delay factor
	MOVB	#1, @#LDC85	; Use delay and copy screen in LBEDE
	MOV	#35036, @#L86D7	; Set penRow/penCol
	MOV	#SE09D, R3	; "MaxCoderz Presents"
	CALL	LBEDE		; Show message char-by-char
	CALL	LBA81		; Delay x40
	CALL	LBC7D		; Clear shadow screen and copy to UKNC screen
	CALL	LBC34		; Delay x20
	MOV	#35056, @#L86D7 ; Set penRow/penCol
	MOV	#SE09F, R3	; "a tr1p1ea game"
	CALL	LBEDE		; Show message char-by-char
	CALL	LBA81		; Delay x40
	CALL	LBC7D		; Clear shadow screen and copy to UKNC screen
	CALL	LBC34		; Delay x20
	CLRB	@#LDC85		; Skip delay and copy screen in LBEDE
;TODO:	CALL	ScreenThemeLite	; switching to the light theme on Main Menu
	BR	LBA3D		; Return to Menu
;
; MenuFromGame:
MENUFG:
	MOV	#<072+12.>, @#LDB8F	; "Continue" menu item -> set Menu Y pos
;
; Return to Menu
;
LBA3D:
	MOVB	@#LDC55, R0	; get Menu background phase
	INC	R0
	BIC	#177770, R0
	MOVB	R0, @#LDC55	; set Menu background phase
;	DI
	MOV	#LF515, R3	; Main menu screen moving background, 96 tiles
	CALL	LA88F		; Display 96 tiles on the screen
	MOV	#LF4B5, R3	; Main menu screen
;	EI
	CALL	LB177		; Display screen HL from tiles with Tileset 2
;TODO
	CALL	SHOWSC		; Copy shadow screen to UKNC screen
	CALL	LA0F1		; Scan keyboard
;TODO

	BR	LBA3D

;
; New Game
;
LBADE:
	CLRB	@#LDCF7		; clear Weapon slot
	CLRB	@#LDB7D		; set look/shoot switch value = Look
	CLRB	@#LDBC7		; clear Items Found count
	CALL	LB9D6		; Clear player variables
	CLR	@#LDBC3		; clear Player deaths count
	CLR	@#LDBC5		; clear Aliens Killed count
	MOV	#LDB9C, R3	; Inventory table address
	MOV	#34., R1	; 34 bytes
LBAF9:	CLRB	(R3)+
	SOB	R1, LBAF9
	MOV	#LDC5B, R3
	MOV	#34., R1
LBB03:	CLRB	(R3)+
	SOB	R1, LBB03
	MOV	#LDB90, R3
	MOV	#9., R1		; 9 variables to clear
LBB09:	CLRB	(R3)+
	SOB	R1, LBB09
	MOV	#LDCA2, R3	; Table with Access code slots
	MOV	#72., R1	; 72 bytes
LBB17:	CLRB	(R3)+
	SOB	R1, LBB17
	MOV	#<DLLENT+1>, R3	;   ld hl,DoorLockLevelEntered+1
	MOV	#4, R1
LBADE5:	CLRB	(R3)+		; Clear 4 flags
	SOB	R1, LBADE5
	MOV	#LDC96, R3	; level 2 access code buffer
	CALL	LBC6B		; Generate random code
	MOV	#LDC9A, R3	; level 3 access code buffer
	CALL	LBC6B		; Generate random code
	MOV	#LDC9E, R3	; level 4 access code buffer
	CALL	LBC6B		; Generate random code
	CALL	LBC7D		; Clear shadow screen and copy to UKNC screen
;TODO:	CALL	ScreenThemeNite	; switching to dark theme for story mode
	MOVB	#68., @#LDC59	; set delay factor
	MOVB	#1, @#LDC85	; Use delay and copy screen in LBEDE
	MOVB	#14., @#LDCF4	; set Line interval for text
	CLRB	@#LDCF3		; clear Left margin size for text
	MOV	#35024, @#L86D7	; Set penRow/penCol
	MOV	#SE115, R3	; "In the Distant Future . . ."
	CALL	LBEDE		; Show message char-by-char
	CALL	LBA81		; Delay x40
	CALL	LBC7D		; Clear shadow screen and copy to UKNC screen
	CALL	LBA81		; Delay x40
	CLR	@#L86D7		; Set zero penRow/penCol
	MOV	#SE117, R3	; "'The Desolate' Space Cruiser leaves orbit. ...
	CALL	LBEDE		; Show message char-by-char
	MOV	#71266, @#L86D7	; Set penRow/penCol
	MOV	#SE0B9, R3	; String with arrow down sign
	CALL	LBEDE		; Show message char-by-char
	CALL	WTKEY		; Wait for any key
	CALL	CLSHAD		; ClearShadowScreen
	CLR	@#L86D7		; Set zero penRow/penCol
	MOV	#SE119, R3	; "The ship sustains heavy damage. ...
	CALL	LBEDE		; Show message char-by-char
	CALL	WTKEY		; Wait for any key
;
; Game start
;
LBB7E:
	CLRB	@#LDC85		; Skip delay and copy screen in LBEDE
; Continue menu item selected
LBB82:
	CALL	LBC7D		; Clear shadow screen and copy to UKNC screen
;TODO:	CALL	ScreenThemeLite	; switching to light theme opening the game screen
	MOVB	#001, @#LDB73
	MOVB	#377, @#LDC59	; set delay factor
	CALL	LB2D0		; Delay
	JMP	L9DDD		; return to the main game loop

;
; Info menu item, show Controls
;
LBBEC:
	MOV	#LF329, R3	; Decode from - Encoded screen for Inventory/Info popup
	CALL	LADEE		; Decode 96 bytes of the screen to LDBF5
	CALL	LB177		; Display screen HL from tiles with Tileset 2
	MOVB	#10., @#LDCF3	; Left margin size for text
	MOVB	#14., @#LDCF4	; Line interval for text
	MOV	#013074, @#L86D7  ; Set penRow/penCol
	MOV	#SE0A5, R3	; "- Controls -"
	CALL	DRSTR		; Show message char-by-char
	MOV	#022012, @#L86D7  ; Set penRow/penCol
	MOV	#SE0A7, R3	; "2nd = Look / Shoot Alpha = Inventory ..."
	CALL	LBEDE		; Show message char-by-char
	CALL	SHOWSC		; Copy shadow screen to UKNC screen
	CALL	LADA1		; Wait for Escape key
	JMP	LBA3D		; Return to Menu
;
; Delay x40
LBA81:
	CALL	LBC34		; Delay x20
; Delay x20
LBC34:
	MOV	#20., R1	; x20
LBC36:
	CALL	LB2D0		; Delay
	SOB	R1, LBC36
	RETURN
;
; Draw access code, 4 chars
;   HL = access code buffer address
LBC3C:
;TODO
	RETURN
;
; Generate random access code
;   HL = 4-byte buffer address
LBC6B:
;TODO
	RETURN
;
; Clear shadow screen and copy to UKNC screen
LBC7D:
	CALL	CLSHAD		; Clear shadow screen
	JMP	SHOWSC		; Copy shadow screen to UKNC screen

;
; Found action point at room description offset $0F..$10
LBC8B:
;TODO
; Show the message, show screen, wait for key, continue game main loop
LBCC5:
;TODO
	JMP	L9E2E		; Show the screen, continue the game main loop

; Draw string on the screen
;   R3 = String address
LBEDE:
	MOVB	(R3)+, R0
	BNE	LBEDE1
	RETURN
LBEDE1:	CMPB	R0, #174	; CP 7Ch	; '|' - line end ?
	BEQ	LBF1B
	PUSH	R3
	CALL	DRCHAR
	MOVB	@#LDC85, R0		; get Delay and copy screen flag
	BEQ	LBEF91
	CALL	LB2D0			; Delay
	CALL	SHOWSC			; Copy shadow screen to UKNC screen
LBEF91:
	POP	R3
	BR	LBEDE
LBF1B:					; Line end
	PUSH	R1
	MOVB	@#L86D8, R1		; Get penRow 0..127
	MOVB	@#LDCF4, R0		; Line interval for text
	ADD	R1, R0
	MOVB	R0, @#L86D8		; Set penRow
	MOVB	@#LDCF3, @#L86D7	; Get left margin size for text -> set penCol
	POP	R1
	BR	LBEDE
;
; Set variables for Credits
LBF54:
	CLRB	@#LDD57		; clear Credits line number
	CLRB	@#LDD56		; clear Credits counter within one line
	CLRB	@#LDC85		; Skip delay and copy screen in LBEDE
	MOVB	#150., @#LDC59	; set delay factor
	RETURN
;
; Credits menu item selected
LBF64:
	CALL	LBC7D		; Clear and show shadow screen
;TODO:	CALL	ScreenThemeNite	; switching to dark theme for Credits
	CALL	LBF54		; Set variables for Credits
	JMP	LBF81		; Credits screen text scrolls up
;
; The End
;
LBF6F:
	CALL	LBC7D		; Clear and show shadow screen
;TODO:	CALL	ScreenThemeNite	; switching to dark theme for End and Credits
	CALL	LBF54		; Set variables for Credits
	MOV	#027106, @#L86D7	; Set penRow/penCol
	MOV	#SE11F, R3	; "The End"
	CALL	LBEDE		; Show message char-by-char
;
; Credits screen text scrolls up
;
LBF81:
	MOVB	#126., @#L86D8	; Set penRow, to draw new strings on the very bottom
LBF686:
	BR	LBF6F4
LBF6F2:
	CALL	SHOWSC		; Copy shadow screen to ZX screen
LBF6F3:
	CALL	GETKEY		; Scan keyboard
	BNE	CREXIT		; any key pressed => Return to main Menu
	CALL	LBFD5		; Scroll shadow screen up one line
LBF6F4:
	MOVB	@#LDD56, R0
	INC	R0		; increase counter within the line
	MOVB	R0, @#LDD56
	CMP	R0, #12.	; last line of the current string?
	BEQ	CREDS5		; yes => jump
	CALL	LB2D0		; Delay
	BR	LBF6F2		; continue the Credits loop
CREDS5:
	CLRB	@#LDD56		; clear counter within the line
	MOVB	@#LDD57, R2
	ADD	#LDDF2, R2	; Table of left margins for Credits strings
	MOVB	(R2), @#L86D7	; Set penCol
	MOVB	@#LDD57, R0	; get Credits line number
	MOV	#LDD58, R3	; Table of Credits strings
	CALL	LADFF		; Get address from table HL by index A
	CALL	DRSTR		; Draw string on shadow screen without any delays
	MOVB	@#LDD57, R0
	INC	R0		; increase the Credits line counter
	MOVB	R0, @#LDD57
	CMP	R0, #73.
	BNE	LBF6F3
	CALL	LBA81		; Delay x40 - added to have a pause after the last line
CREXIT:
	CALL	LBC7D		; Clear and show shadow screen
;TODO:	CALL	ScreenThemeLite	; switching to light theme for Main Menu
	JMP	LBA3D		; Return to main Menu
;
; Scroll shadow screen up 1px
LBFD5:
	MOV	#SHADOW, R2
	MOV	#<SHADOW+24.>, R3
	MOV	#414., R1	; 138 * 24 / 8 = 414.
LBFD51:
	MOV	(R3)+, (R2)+
	MOV	(R3)+, (R2)+
	MOV	(R3)+, (R2)+
	MOV	(R3)+, (R2)+
	SOB	R1, LBFD51
	RETURN

;----------------------------------------------------------------------------
